apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "org.emunix.unipatcher"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 170002
        versionName "0.17"
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-21", "-DCMAKE_BUILD_TYPE=Release",
                        "-DANDROID_ARM_MODE=arm"
                version "3.10.2"
            }
        }
    }

    ndkVersion "21.3.6528147"

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFile './proguard-android.txt'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-DEBUG'
        }
    }

    flavorDimensions "default"

    productFlavors {
        free {
            buildConfigField "String", "RATE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "PAYPAL_USER", "\"btimofeev@emunix.org\""
            buildConfigField "String", "PAYPAL_CURRENCY_CODE", "\"USD\""
            buildConfigField "String", "BITCOIN_ADDRESS", "\"16coztryz7xbNNDNhhf98wuHmi3hEintsW\""
        }
        google {
            buildConfigField "String", "RATE_URL", "\"market://details?id=org.emunix.unipatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://play.google.com/store/apps/details?id=org.emunix.unipatcher\""
            buildConfigField "String", "GOOGLE_PLAY_PUBKEY", "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEA384jTCBEuJ8nCWaC4S6AFrnMQN4mBlmkOXHV3Xg5hlFOl8TkVwiCfqz8r20yJpEy0IJ1+3QRnlq59zadUxbkD+PacJlGB/r2b3mbKfu+m0K+e/0aL6eWupjMSIyPgpnbN3uswiBEGUb4ytzYF53ZKTbLARnruQdMnjV6+VyfwMgpor/48anVQawDARBj/AIAj6VGtRHLmg6DmKDyOGQ7uCgXSv+ysnBKJjtIX/L/5nQgL8Q+9jsr2knuWY7j9BmrtpUXaDH3Kb50M1TOCKiqxPGa8lInOOIndABWxcpqmSMXP06SPYOanUlEH7lT0jjqpHpFNx8hRTT9xf652rgMJwIDAQAB\""
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.3.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.core:core:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    def coroutines_version = "1.3.9"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'commons-io:commons-io:2.5' // 2.6 crashes the application on Android below 8, because java.nio.file is not available in these versions

    implementation 'com.github.penn5:donations:3.4.5'
    implementation 'org.sufficientlysecure:html-textview:3.9'

    implementation 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // DI
    implementation 'com.google.dagger:dagger:2.28'
    kapt 'com.google.dagger:dagger-compiler:2.28'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    kapt 'com.android.databinding:compiler:3.1.4'

    // Crash reports
    def acraVersion = '5.6.1'
    implementation "ch.acra:acra-mail:$acraVersion"
    implementation "ch.acra:acra-notification:$acraVersion"
}

repositories {
    mavenCentral()
}

task deleteDependencies(type: Delete) {
    delete 'src/main/cpp/xdelta/xdelta'
    delete 'src/main/cpp/xz/xz'
}

task downloadDependencies(dependsOn: deleteDependencies) {
    def XDelta = '3.1.0'
    def xz = '5.2.4'

    doLast {
        download {
            src "https://github.com/jmacd/xdelta/archive/v${XDelta}.tar.gz"
            dest new File(buildDir, "xdelta-${XDelta}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/xdelta-${XDelta}.tar.gz"))
            into 'src/main/cpp/xdelta/'
        }
        file("src/main/cpp/xdelta/xdelta-${XDelta}").renameTo(file('src/main/cpp/xdelta/xdelta'))
        delete("src/main/cpp/xdelta/pax_global_header")

        download {
            src "https://sourceforge.net/projects/lzmautils/files/xz-${xz}.tar.gz/download"
            dest new File(buildDir, "xz-${xz}.tar.gz")
        }
        copy {
            from tarTree(resources.gzip("${buildDir}/xz-${xz}.tar.gz"))
            into 'src/main/cpp/xz/'
        }
        file("src/main/cpp/xz/xz-${xz}").renameTo(file('src/main/cpp/xz/xz'))
    }
}
