apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    signingConfigs {
        release
    }

    defaultConfig {
        applicationId "org.emunix.unipatcher"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 160000
        versionName "0.16"
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
                arguments "-DANDROID_PLATFORM=android-14", "-DCMAKE_BUILD_TYPE=Release"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources false
            proguardFile './proguard-android.txt'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "default"

    productFlavors {
        free {
            buildConfigField "String", "RATE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://github.com/btimofeev/UniPatcher\""
            buildConfigField "String", "PAYPAL_USER", "\"btimofeev@emunix.org\""
            buildConfigField "String", "PAYPAL_CURRENCY_CODE", "\"USD\""
            buildConfigField "String", "BITCOIN_ADDRESS", "\"16coztryz7xbNNDNhhf98wuHmi3hEintsW\""
        }
        google {
            buildConfigField "String", "RATE_URL", "\"market://details?id=org.eminix.unipatcher\""
            buildConfigField "String", "SHARE_URL", "\"https://play.google.com/store/apps/details?id=org.eminix.unipatcher\""
            buildConfigField "String", "GOOGLE_PLAY_PUBKEY", "\"MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEA384jTCBEuJ8nCWaC4S6AFrnMQN4mBlmkOXHV3Xg5hlFOl8TkVwiCfqz8r20yJpEy0IJ1+3QRnlq59zadUxbkD+PacJlGB/r2b3mbKfu+m0K+e/0aL6eWupjMSIyPgpnbN3uswiBEGUb4ytzYF53ZKTbLARnruQdMnjV6+VyfwMgpor/48anVQawDARBj/AIAj6VGtRHLmg6DmKDyOGQ7uCgXSv+ysnBKJjtIX/L/5nQgL8Q+9jsr2knuWY7j9BmrtpUXaDH3Kb50M1TOCKiqxPGa8lInOOIndABWxcpqmSMXP06SPYOanUlEH7lT0jjqpHpFNx8hRTT9xf652rgMJwIDAQAB\""
        }
        amazon {
            buildConfigField "String", "RATE_URL", "\"amzn://apps/android?p=org.emunix.unipatcher\""
            buildConfigField "String", "SHARE_URL", "\"http://www.amazon.com/gp/mas/dl/android?p=org.emunix.unipatcher\""
            buildConfigField "String", "PAYPAL_USER", "\"btimofeev@emunix.org\""
            buildConfigField "String", "PAYPAL_CURRENCY_CODE", "\"USD\""
            buildConfigField "String", "BITCOIN_ADDRESS", "\"16coztryz7xbNNDNhhf98wuHmi3hEintsW\""
        }
        slideme {
            buildConfigField "String", "RATE_URL", "\"sam://details?id=org.emunix.unipatcher\""
            buildConfigField "String", "SHARE_URL", "\"http://slideme.org/application/unipatcher\""
            buildConfigField "String", "PAYPAL_USER", "\"btimofeev@emunix.org\""
            buildConfigField "String", "PAYPAL_CURRENCY_CODE", "\"USD\""
            buildConfigField "String", "BITCOIN_ADDRESS", "\"16coztryz7xbNNDNhhf98wuHmi3hEintsW\""
        }
    }

    sourceSets{
        amazon.java.srcDir 'src/free/java'
        slideme.java.srcDir 'src/free/java'
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def Properties props = new Properties()
def propFile = file('../../signing.properties')
if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {

        println 'RELEASE BUILD SIGNING'

        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'RELEASE BUILD NOT FOUND SIGNING PROPERTIES'

        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'RELEASE BUILD NOT FOUND SIGNING FILE'
    android.buildTypes.release.signingConfig = null
}

dependencies {
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.12.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:preference-v14:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    // used in material-dialogs
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.sufficientlysecure:donations:2.5'
    implementation 'org.sufficientlysecure:html-textview:3.5'
    implementation 'org.commonjava.googlecode.markdown4j:markdown4j:2.2-cj-1.1'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
repositories {
    mavenCentral()
}
